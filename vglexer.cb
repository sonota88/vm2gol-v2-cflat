// -*- mode: c -*-

import lib.common_import;
import lib.utils;

#define INPUT_SIZE 8192

int is_space_char(char ch) {
  return (
       char_eq(ch, ' ')
    || char_eq(ch, '\n')
  );
}

int match_space(char* rest) {
  int i;
  for (i = 0; i < strlen(rest); i++) {
    if (is_space_char(rest[i])) {
      ;
    } else {
      break;
    }
  }
  return i;
}

int match_comment(char* rest) {
  if (strncmp(rest, "//", 2) != 0) {
    return 0;
  }

  return find_index(rest, '\n', 2);
}

int match_str(char* rest) {
  if (! char_eq(rest[0], '"')) {
    return 0;
  }

  return find_index(rest, '"', 1) - 1;
}

int match_int(char* rest) {
  if (
       char_eq(rest[0], '-')
    || is_numeric(rest[0])
  ) {
    // matched
  } else {
    return 0;
  }

  return find_non_numeric_index(rest);
}

int match_symbol(char* str) {
  if (
       strncmp(str, "==", 2) == 0
    || strncmp(str, "!=", 2) == 0
  ) {
    return 2;
  } else if (match_any_char(";(){},+*=", str[0])) {
    return 1;
  } else {
    return 0;
  };
}

int is_ident_char(char ch) {
  return (
       ('a' <= ch && ch <= 'z')
    || ('0' <= ch && ch <= '9')
    || ch == '_'
  );
}

int match_ident(char* rest) {
  int i;
  for (i = 1; i < strlen(rest); i++) {
    if (is_ident_char(rest[i])) {
      ;
    } else {
      break;
    }
  }
  return i;
}

int is_kw(char* str) {
  return (
       str_eq(str, "func")
    || str_eq(str, "set")
    || str_eq(str, "var")
    || str_eq(str, "call_set")
    || str_eq(str, "call")
    || str_eq(str, "return")
    || str_eq(str, "while")
    || str_eq(str, "case")
    || str_eq(str, "_cmt")
    || str_eq(str, "_debug")
  );
}

int main(void) {
  char[INPUT_SIZE] input;
  int pos = 0;
  int src_len;
  char[INPUT_SIZE] rest;
  char[256] temp;
  int size;

  read_stdin_all(input);

  src_len = strlen(input);

  while (pos < src_len) {
    substring(rest, input, pos, INPUT_SIZE);

    size = match_space(rest);
    if (0 < size) {
      pos += size;
      continue;
    }

    size = match_comment(rest);
    if (0 < size) {
      pos += size;
      continue;
    }

    size = match_str(rest);
    if (0 < size) {
      substring(temp, rest, 1, size + 1);
      printf("str:%s\n", temp);
      pos += size + 2;
      continue;
    }

    size = match_int(rest);
    if (0 < size) {
      substring(temp, rest, 0, size);
      printf("int:%s\n", temp);
      pos += size;
      continue;
    }

    size = match_symbol(rest);
    if (0 < size) {
      substring(temp, rest, 0, size);
      printf("sym:%s\n", temp);
      pos += size;
      continue;
    }

    size = match_ident(rest);
    if (0 < size) {
      substring(temp, rest, 0, size);
      if (is_kw(temp)) {
        printf("kw:%s\n", temp);
      } else {
        printf("ident:%s\n", temp);
      }
      pos += size;
      continue;
    }

    fprintf(stderr, "Unexpected pattern (%s)\n", rest);
    exit(1);
  }

  return 0;
}
